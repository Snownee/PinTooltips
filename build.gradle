plugins {
    alias(libs.plugins.fabric.loom)
    id "me.shedaniel.unified-publishing" version "0.1.+"
}

group = "snownee.pintooltips"
version = "${libs.versions.minecraft.get()}-Fabric-${mod_version}"
var realVersion = project.mod_version + '+fabric'
archivesBaseName = project.archives_base_name

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

loom {
    mixin {
        defaultRefmapName = "${mod_id}.refmap.json"
    }

//    accessWidenerPath = file("src/main/resources/${mod_id}.accesswidener")

    runs {
        configureEach {
            property "mixin.debug.export", "true"
            property "mixin.debug.countInjection", "true"
            property "mixin.debug.verbose", "true"
            property "mixin.dumpTargetOnFailure", "true"
        }
    }
}

repositories {
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }

    maven {
        url = "https://maven.shedaniel.me/"
        content {
            includeGroup("me.shedaniel.cloth")
        }
    }

    maven {
        name = "Terraformers"
        url = "https://maven.terraformersmc.com/"

        content {
            includeGroup("com.terraformersmc")
            includeGroup("me.shedaniel.cloth")
        }
    }

    maven {
        name = 'ParchmentMC'
        url = 'https://maven.parchmentmc.org'
    }
}

dependencies {
    // To change the versions see the gradle/libs.version.toml
    minecraft libs.minecraft
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-1.20.1:${libs.versions.parchmentmc.get()}@zip")
    }

    modImplementation libs.fabric.loader
    modApi libs.fabric.api

    modRuntimeOnly(libs.modmenu)
    modRuntimeOnly(libs.cloth.config) {
        exclude(group: "net.fabricmc.fabric-api")
    }

    modRuntimeOnly("maven.modrinth:prism-lib:1.0.5-fabric")
    modRuntimeOnly("maven.modrinth:iceberg:1.1.25-fabric")
    modRuntimeOnly("maven.modrinth:forge-config-api-port:v8.0.0-1.20.1-Fabric")
    modRuntimeOnly("maven.modrinth:equipment-compare:1.3.8-fabric,1.20.1")
    modRuntimeOnly("maven.modrinth:legendary-tooltips:1.4.5-fabric,1.20.1")
    modImplementation("maven.modrinth:jei:15.20.0.105")
    modImplementation("maven.modrinth:just-enough-effect-descriptions-jeed:1.20-2.2.2")
}

processResources {
    def properties = [
            version        : realVersion,
            id             : mod_id,
            name           : mod_name,
            mod_description: mod_description,
            source         : "https://github.com/Snownee/PinTooltips",
            minecraft      : ">=1.20.1"
    ]
    filesMatching(["fabric.mod.json", "*.mixins.json"]) {
        expand(properties)
    }
}

unifiedPublishing {
    project {
        displayName = "[Fabric $project.supported_version] $project.mod_version"
        version = realVersion // Optional, Inferred from project by default
        changelog = file("CHANGELOG.md").exists() ? file("CHANGELOG.md").text : "" // Optional, in markdown format
        releaseType = project.release_type // Optional, use "release", "beta" or "alpha"
        gameVersions = [project.minecraft_version]
        gameLoaders = ["fabric", "quilt"]

        mainPublication tasks.remapJar // Declares the publicated jar

        if (System.getenv("CURSE_TOKEN") != null) {
            curseforge {
                token = System.getenv("CURSE_TOKEN")
                id = "1131483"

                relations { // Optional, Inferred from the relations above by default
                    optional "just-enough-effect-descriptions-jeed"
                }
            }
        }

        if (System.getenv("MODRINTH_TOKEN") != null) {
            modrinth {
                token = System.getenv("MODRINTH_TOKEN")
                id = "fFmpgMfg"

                relations { // Optional, Inferred from the relations above by default
                    optional "just-enough-effect-descriptions-jeed"
                }
            }
        }
    }
}